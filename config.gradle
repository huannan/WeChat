def versions = [:]
versions.androidx_core_ktx = "1.3.2"
versions.androidx_appcompat = "1.2.0"
versions.androidx_constraintlayout = "2.0.4"
versions.androidx_junit = "1.1.2"
versions.java_junit = "4.12"
versions.androidx_espresso_core = "3.3.0"
ext.versions = versions

def build_versions = [:]
build_versions.min_sdk = 21
build_versions.target_sdk = 30
build_versions.compile_sdk = 30
build_versions.android_gradle_plugin = "4.0.2"
build_versions.build_tools = "30.0.3"
build_versions.kotlin = "1.4.21"
// 主模块
build_versions.app_version_name = "1.0.0"
build_versions.app_version_code = 1000000
// 登录模块
build_versions.login_version_name = "1.0.0"
build_versions.login_version_code = 1000000
// 朋友圈模块
build_versions.moments_version_name = "1.0.0"
build_versions.moments_version_code = 1000000
ext.build_versions = build_versions

def build_configs = [:]
build_configs.is_run_alone = isRunAlone()
ext.build_configs = build_configs

def addRepos(RepositoryHandler handler) {
    handler.maven { url 'https://maven.aliyun.com/repository/public/' }
    handler.flatDir { dir new File(rootProject.rootDir, 'third_libs') }
    handler.jcenter()
    handler.google()
}

ext.addRepos = this.&addRepos

/**
 * 修改本地文件local.properties中的IS_RUN_ALONE以控制是否为开发模式或集成模式
 * 参考配置如下：
 * sdk.dir=/home/meizu/Android/Sdk
 * ndk.dir=/home/meizu/Android/Sdk/ndk/android-ndk-r16
 * IS_RUN_ALONE=false
 */
def isRunAlone() {
    def isRunAlone = false
    def file = rootProject.file('local.properties')
    if (file.exists() && file.isFile()) {
        Properties properties = new Properties()
        properties.load(file.newDataInputStream())
        isRunAlone = Boolean.parseBoolean(properties.getProperty('IS_RUN_ALONE', 'false'))
    }
    return isRunAlone
}